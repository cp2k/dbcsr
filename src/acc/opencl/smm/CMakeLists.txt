set(DBCSR_OPENCL_KERNELS_INC ${CMAKE_CURRENT_SOURCE_DIR}/opencl_kernels.h)

set(DBCSR_OPENCL_KERNELS kernels/multiply.cl kernels/transpose.cl)
list(TRANSFORM DBCSR_OPENCL_KERNELS PREPEND ${CMAKE_CURRENT_SOURCE_DIR}/)

set(DBCSR_OPENCL_PARAMS_WITHGPU
    ${CMAKE_CURRENT_SOURCE_DIR}/params/tune_multiply_${WITH_GPU}.csv)
set(DBCSR_OPENCL_PARAMS_CUSTOM ${CMAKE_CURRENT_SOURCE_DIR}/tune_multiply.csv)

if (EXISTS ${DBCSR_OPENCL_PARAMS_WITHGPU})
  set(DBCSR_OPENCL_SCRIPT_MSG
      "ACC/LIBSMM OpenCL: using parameters for ${WITH_GPU}")
  set(DBCSR_OPENCL_PARAMS ${DBCSR_OPENCL_PARAMS_WITHGPU})
elseif (WITH_GPU MATCHES "none")
  set(DBCSR_OPENCL_SCRIPT_MSG "ACC/LIBSMM OpenCL: no tuned parameters used")
  set(DBCSR_OPENCL_SCRIPT_ARGS -p \"\")
elseif (EXISTS ${DBCSR_OPENCL_PARAMS_CUSTOM})
  set(DBCSR_OPENCL_SCRIPT_MSG "ACC/LIBSMM OpenCL: using custom parameters")
  set(DBCSR_OPENCL_PARAMS ${DBCSR_OPENCL_PARAMS_CUSTOM})
else ()
  set(DBCSR_OPENCL_SCRIPT_MSG "ACC/LIBSMM OpenCL: using all tuned parameters")
endif ()
message(STATUS ${DBCSR_OPENCL_SCRIPT_MSG})

add_custom_target(
  parameters # ALL
  COMMAND
    ${DBCSR_OPENCL_SCRIPT} ${DBCSR_OPENCL_SCRIPT_ARGS} ${DBCSR_OPENCL_KERNELS}
    ${DBCSR_OPENCL_PARAMS} ${DBCSR_OPENCL_KERNELS_INC}
  DEPENDS ${DBCSR_OPENCL_SCRIPT} ${DBCSR_OPENCL_KERNELS} ${DBCSR_OPENCL_COMMON}
  BYPRODUCTS ${DBCSR_OPENCL_KERNELS_INC}
  COMMENT ${DBCSR_OPENCL_SCRIPT_MSG})

add_dependencies(dbcsr parameters)
target_include_directories(dbcsr PRIVATE ${CMAKE_CURRENT_BINARY_DIR}
                                         ${CMAKE_CURRENT_SOURCE_DIR})
